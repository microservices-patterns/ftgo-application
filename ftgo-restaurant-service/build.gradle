
apply plugin: FtgoServicePlugin

apply plugin: IntegrationTestsPlugin

apply plugin: 'spring-cloud-contract'

contracts {
    contractsDslDir = new File("../ftgo-restaurant-service-contracts/src/main/resources/contracts")
    packageWithBaseClasses = 'net.chrisrichardson.ftgo.restaurantservice.contract'
    generatedTestSourcesDir = project.file("${project.buildDir}/generated-integration-test-sources/contracts")
    sourceSet = "integrationTest"
}

sourceSets {
    integrationTest {
        java {
            srcDir project.file("${project.buildDir}/generated-integration-test-sources/contracts")
        }
    }
}

compileTestGroovy.enabled=false

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:$springCloudContractDependenciesVersion"
    }
}

configurations.all {
    // Out of date, conflicting JSON library
    exclude  group: "com.vaadin.external.google"
}

dependencies {


    implementation "io.eventuate.tram.core:eventuate-tram-spring-jdbc-kafka"
    implementation "io.eventuate.tram.core:eventuate-tram-spring-events"
    implementation "io.eventuate.tram.core:eventuate-tram-spring-messaging"
    implementation "io.eventuate.tram.sagas:eventuate-tram-sagas-spring-orchestration-simple-dsl"
    implementation "io.eventuate.tram.core:eventuate-tram-aggregate-domain-events"

    implementation project(":common-swagger")
    implementation project(":ftgo-restaurant-service-api")
    implementation project(":ftgo-restaurant-service-api-spec")
    implementation project(":ftgo-common-jpa")
    implementation project(":ftgo-common")

    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"

    implementation 'javax.el:javax.el-api:2.2.5'

    implementation('org.apache.kafka:kafka-clients:2.3.0') {
        force = true
    }

    testImplementation "io.eventuate.util:eventuate-util-test"
    testImplementation "io.eventuate.tram.core:eventuate-tram-test-util"

    testImplementation "io.eventuate.tram.sagas:eventuate-tram-sagas-spring-in-memory"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "com.jayway.restassured:rest-assured:$restAssuredVersion"
    testImplementation "com.jayway.jsonpath:json-path:2.3.0"
    testImplementation "io.eventuate.tram.core:eventuate-tram-spring-testing-support-cloud-contract"
    testImplementation project(":ftgo-test-util-json-schema")

    integrationTestImplementation "org.springframework.cloud:spring-cloud-contract-wiremock"
    integrationTestImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner"
    integrationTestImplementation "io.eventuate.tram.sagas:eventuate-tram-sagas-spring-testing-support"
    integrationTestImplementation "io.eventuate.tram.core:eventuate-tram-spring-in-memory"

}
