buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:$springDependencyManagementPluginVersion"
        // if using Stub Runner (consumer side) only remove this dependency
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:$springCloudContractDependenciesVersion"
    }
}

apply plugin: "io.spring.dependency-management"
apply plugin: 'spring-cloud-contract'
apply plugin: IntegrationTestsPlugin
apply plugin: FtgoJSONSchema2PojoPlugin

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:$springCloudContractDependenciesVersion"
    }
}

contracts {
    contractsDslDir = new File("../ftgo-kitchen-service-contracts/src/main/resources/contracts")
    packageWithBaseClasses = 'net.chrisrichardson.ftgo.kitchenservice.contract'
    generatedTestSourcesDir = project.file("${project.buildDir}/generated-integration-test-sources/contracts")
    sourceSet = "integrationTest"
}

sourceSets {
    integrationTest {
        java {
            srcDir project.file("${project.buildDir}/generated-integration-test-sources/contracts")
        }
    }
}

compileTestGroovy.enabled=false

apply plugin: FtgoServicePlugin


dependencies {

    ftgoApiSpecification project(":ftgo-restaurant-service-api-spec")

    implementation "io.eventuate.tram.core:eventuate-tram-spring-jdbc-kafka"
    implementation "io.eventuate.tram.core:eventuate-tram-spring-events"
    implementation "io.eventuate.tram.core:eventuate-tram-spring-messaging"
    implementation "io.eventuate.tram.sagas:eventuate-tram-sagas-spring-orchestration-simple-dsl"

    implementation project(":common-swagger")
    implementation project(":ftgo-common-jpa")
    implementation project(":ftgo-kitchen-service-api")
    implementation project(":ftgo-restaurant-service-api")
    implementation "io.eventuate.tram.core:eventuate-tram-aggregate-domain-events"

    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation 'javax.el:javax.el-api:2.2.5'

    implementation('org.apache.kafka:kafka-clients:2.3.0') {
        force = true
    }

    testImplementation "io.eventuate.util:eventuate-util-test"
    testImplementation "io.eventuate.tram.core:eventuate-tram-test-util"

    testImplementation "io.eventuate.tram.sagas:eventuate-tram-sagas-spring-in-memory"
    testImplementation "io.eventuate.tram.core:eventuate-tram-spring-in-memory"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "com.jayway.restassured:rest-assured:$restAssuredVersion"
    testImplementation "com.jayway.jsonpath:json-path:2.3.0"
    testImplementation "io.eventuate.tram.core:eventuate-tram-spring-testing-support-cloud-contract"

    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}

ftgoJsonSchema2Pojo {

    ftgoRestaurantService {
        source = files("${ftgoApiSpecsDir}/ftgo-restaurant-service-api-spec/messages")
        targetPackage = "net.chrisrichardson.ftgo.restaurantservice.events"
        includeAdditionalProperties = false
        generateBuilders = true
        useLongIntegers = true
    }

}
