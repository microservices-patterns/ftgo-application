
apply plugin: FtgoServicePlugin

// apply plugin: 'spring-cloud-contract'

apply plugin: 'docker-compose'

apply plugin: IntegrationTestsPlugin
apply plugin: ComponentTestsPlugin
apply plugin: FtgoJSONSchema2PojoPlugin

dependencies {
    implementation 'io.eventuate.tram.sagas:eventuate-tram-sagas-participant:0.20.0.RELEASE'
    implementation 'io.eventuate.tram.sagas:eventuate-tram-sagas-participant:0.20.0.RELEASE'
    implementation 'io.eventuate.tram.sagas:eventuate-tram-sagas-spring-participant:0.20.0.RELEASE'
    ftgoApiSpecification project(":ftgo-restaurant-service-api-spec")

    implementation project(":ftgo-delivery-service-api")
    implementation project(":ftgo-kitchen-service-api")
    implementation project(":ftgo-restaurant-service-api")
    implementation project(":ftgo-order-service-api")
    implementation project(":ftgo-common")
    implementation project(":ftgo-common-jpa")
    implementation project(":common-swagger")

    implementation "io.eventuate.tram.core:eventuate-tram-spring-events"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "io.eventuate.tram.core:eventuate-tram-spring-jdbc-kafka"

    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    implementation "io.micrometer:micrometer-registry-prometheus:$micrometerVersion"

    implementation('org.apache.kafka:kafka-clients:2.3.0') {
        force = true
    }

    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "io.eventuate.tram.core:eventuate-tram-spring-in-memory"
    testImplementation "io.eventuate.util:eventuate-util-test"

    componentTestImplementation "com.jayway.restassured:rest-assured:$restAssuredVersion"
    componentTestImplementation "com.jayway.jsonpath:json-path:2.3.0"
    componentTestImplementation "org.hamcrest:hamcrest:2.1"
    componentTestImplementation project(":ftgo-test-util")

}

dockerCompose {
    environment.put "EVENTUATE_COMMON_VERSION", eventuateCommonImageVersion
    environment.put "EVENTUATE_CDC_VERSION", eventuateCdcImageVersion
    environment.put "EVENTUATE_SAGA_VERSION", eventuateTramSagasImageVersion
    environment.put "EVENTUATE_JAVA_BASE_IMAGE_VERSION", eventuateExamplesBaseImageVersion
    environment.put "EVENTUATE_MESSAGING_KAFKA_IMAGE_VERSION", eventuateMessagingKafkaImageVersion
    
    projectName = null

    componentTests {
        projectName = null
        removeOrphans = true
        retainContainersOnStartupFailure = true

        startedServices = [ 'ftgo-delivery-service']
        stopContainers = true
    }

}

componentTestsComposeUp.dependsOn(assemble)
dockerCompose.componentTests.isRequiredBy(componentTest)

ftgoJsonSchema2Pojo {

    ftgoRestaurantService {
        source = files("${ftgoApiSpecsDir}/ftgo-restaurant-service-api-spec/messages")
        targetPackage = "net.chrisrichardson.ftgo.restaurantservice.events"
        includeAdditionalProperties = false
        generateBuilders = true
        useLongIntegers = true
    }

}

